;
;     SysTray library documentation
;
;      (c) 2024 - Fantaisie Software
;

@Library SysTray

@Overview
  "SysTray" ist der rechte Abschnitt der Taskleiste, welcher einige Icons
  und Datum/Uhrzeit enthält. PureBasic bietet vollen Zugriff auf diesen Bereich
  und erlaubt das Hinzufügen einer beliebigen Anzahl von Bildern.
  @LineBreak
  @LineBreak
  Auf einigen Linux-Distributionen (wie Ubuntu) können die Systray-Icons standardmäßig
  auch versteckt sein. Für weitere Informationen siehe diesen
  @InternetLink "http://ubuntugenius.wordpress.com/2011/06/25/ubuntu-11-04-fix-show-all-iconsindicators-in-unity-panels-notification-area/" "Link".

@CommandList

@ExampleFile All SysTray.pb

@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function Ergebnis = AddSysTrayIcon(#SysTrayIcon, WindowID, ImageID)

@Description
  Fügt ein Icon in den "SysTray" Bereich hinzu.
@LineBreak
@LineBreak
  Wenn ein Ereignis auf irgendeinem der SysTray Icons auftritt, wird ein @#PB_Event_SysTray
  Ereignis zurückgegeben. @@EventGadget kann benutzt werden, um das benutzte
  SysTrayIcon zu ermitteln. Der @@EventType Befehl wird ebenfalls durch diesen
  Befehl aktualisiert.

@Parameter "#SysTrayIcon"
  Eine Nummer zum Identifizieren des neuen Systray-Icons.
  @ReferenceLink "purebasic_objects" "#PB_Any" kann verwendet werden, um diese Zahl
  automatisch zu generieren.
  
@Parameter "WindowID"
  Das Fenster, auf dem die Systray-Ereignisse verwaltet werden. @@WindowID kann
  verwendet werden, um diesen Wert zu erhalten.
  
@Parameter "ImageID"
  Das für das Systray-Icon zu verwendende Bild.
  Es ist ratsam, ein PNG-Bild zu verwenden, um einen Transparenz-Layer zu haben.
  @@ImageID kann benutzt werden, um einfach diese ID zu ermitteln.

@ReturnValue
  Ein Wert ungleich Null, wenn das Systray-Icon erfolgreich hinzugefügt wurde,
  andernfalls Null.

@Remarks
  Alle SysTray-Icons werden automatisch entfernt, wenn das Programm endet.

@Example
@Code
  ; Unsichtbares Fenster, um nur den Systray anzuzeigen
  OpenWindow(0, 0, 0, 10, 10, "", #PB_Window_Invisible)

  UsePNGImageDecoder()
  AddSysTrayIcon(0, WindowID(0), LoadImage(0, #PB_Compiler_Home + "examples/sources/Data/world.png"))

  ; Erstellt ein Popup-Menü, das im Systray im Systray-Look angezeigt wird
  CreatePopupImageMenu(0, #PB_Menu_SysTrayLook)
    MenuItem(0, "About PureBasic...")
    MenuBar()
    MenuItem(1, "Exit")

  ; Verknüpft das Menü mit dem Systray
  SysTrayIconMenu(0, MenuID(0))

  Repeat
    Select WaitWindowEvent()
      Case #PB_Event_Menu
        Select EventMenu()
          Case 1 ; Exit 
            RemoveSysTrayIcon(0)
            End
        EndSelect
    EndSelect
  ForEver
@EndCode

@SeeAlso
  @@RemoveSysTrayIcon, @@ChangeSysTrayIcon
  
@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function ChangeSysTrayIcon(#SysTrayIcon, ImageID)

@Description
  Ändert das angegebene Icon im SysTray-Bereich.
  
@Parameter "#SysTrayIcon"
  Das zu ändernde Systray-Icon.
  
@Parameter "ImageID"
  Das neue Bild, welches für das Systray-Icon verwendet werden soll.
  Es ist ratsam, ein PNG-Bild zu verwenden, um einen Transparenz-Layer zu haben.
  @@ImageID kann benutzt werden, um einfach diese ID zu ermitteln.

@NoReturnValue

@Example
@Code
  ; Unsichtbares Fenster, um nur den Systray anzuzeigen
  OpenWindow(0, 0, 0, 10, 10, "", #PB_Window_Invisible)
  
  ; Erstellt ein Bild mit einer gelben Box, welches für das Systray-Icon verwendet wird:
  If CreateImage(0, 16, 16) And StartDrawing(ImageOutput(0))
    Box(0, 0, 16 , 16, $00DDDD)
    StopDrawing()
  EndIf
  
  AddSysTrayIcon(0, WindowID(0), ImageID(0))
  
  ; Erstellt ein Pop-up Menü, welches später mit dem Systray Icon verwendet wird:
  CreatePopupImageMenu(0, #PB_Menu_SysTrayLook)
    MenuItem(0, "Exit")
    
  ; Verknüpft das Menü mit dem Systray
  SysTrayIconMenu(0, MenuID(0))
  
  AddWindowTimer(0, 0, 1000) ; Fügt einen Window-Timer hinzu, der einen Wechsel des Systray Icons aller 1000 ms auslösen soll
  
  Repeat
    Select WaitWindowEvent()
      Case #PB_Event_Timer
        ; Aktualisiert das Bild des Systray Icons mit einem zufälligen roten Kreis auf gelbem Hintergrund
        If StartDrawing(ImageOutput(0))
          Box(0, 0, 16 , 16, $00DDDD)
          Radius = Random(6, 2)
          Circle(8, 8, Radius, $0000DD)
          StopDrawing()
          ChangeSysTrayIcon(0, ImageID(0))
        EndIf
        
      Case #PB_Event_Menu
        Select EventMenu()
          Case 0 ; Programm beenden nach einem Klick auf das 'Exit'-Menü, welches mit dem Systray-Icon verknüpft wurde
            RemoveSysTrayIcon(0)
            End
        EndSelect
    EndSelect
  ForEver
@EndCode

@SeeAlso
  @@AddSysTrayIcon
  
@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function Ergebnis = IsSysTrayIcon(#SysTrayIcon)

@Description
  Überprüft, ob das angegebene Systray-Icon gültig und korrekt initialisiert ist.

@Parameter "#SysTrayIcon"
  Das zu überprüfende Systray-Icon.

@ReturnValue
  Ein Wert ungleich Null, wenn das Systray-Icon gültig ist, andernfalls Null.

@Remarks
  Diese Funktion ist "kugelsicher" und kann mit jedem Wert benutzt werden. Dies ist der
  richtige Weg, um sicher zu stellen, dass ein SysTray-Icon bereit zur Verwendung ist.

@SeeAlso
  @@AddSysTrayIcon
  
@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function SysTrayIconMenu(#SysTrayIcon, MenuID)

@Description
  Ordnet das angegebene Popup-Menü dem SysTray-Icon zu. Das Menü sollte mit @@CreatePopupImageMenu
  unter Verwendung des Flags @#PB_Menu_SysTrayLook erstellt werden. Dies ist die beste Möglichkeit,
  ein Popup-Menü mit einem SysTray-Icon zu verknüpfen, da das Popup-Menü auf allen Betriebssystemen
  an der richtigen Position angezeigt wird, wenn der Benutzer auf das Icon klickt.

@Parameter "#SysTrayIcon"
  Das zu verwendende Systray-Icon.

@Parameter "MenuID"
  Das Popup-Menü, das mit dem SysTray verknüpft werden soll.
  @@MenuID kann verwendet werden, um diesen Wert abzurufen.

@NoReturnValue

@Example
@Code
  ; Unsichtbares Fenster, um nur den Systray anzuzeigen
  OpenWindow(0, 0, 0, 10, 10, "", #PB_Window_Invisible)

  UsePNGImageDecoder()
  AddSysTrayIcon(0, WindowID(0), LoadImage(0, #PB_Compiler_Home + "examples/sources/Data/world.png"))

  ; Erstellt ein Pop-up Menü, welches im Systray mit Systray-Look angezeigt werden soll
  CreatePopupImageMenu(0, #PB_Menu_SysTrayLook)
    MenuItem(0, "About PureBasic...")
    MenuBar()
    MenuItem(1, "Exit")

  ; Verknüpft das Menü mit dem Systray
  SysTrayIconMenu(0, MenuID(0))

  Repeat
    Select WaitWindowEvent()
      Case #PB_Event_Menu
        Select EventMenu()
          Case 1 ; Exit 
            RemoveSysTrayIcon(0)
            End
        EndSelect
    EndSelect
  ForEver
@EndCode

@SeeAlso
  @@CreatePopupImageMenu
  
@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function SysTrayIconToolTip(#SysTrayIcon, Text$)

@Description
  Verknüpft den angegebenen Text$ mit dem SysTray-Icon. Tooltip-Text ist der Text, welcher (in einer
  gelben Box) angezeigt wird, wenn der Mauspfeil eine Weile über dem Systray-Icon verharrt.

@Parameter "#SysTrayIcon"
  Das zu verwendende Systray-Icon.

@Parameter "Text$"
  Der für den Systray-Icon-Tooltip zu verwendende Text.

@NoReturnValue

@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function RemoveSysTrayIcon(#SysTrayIcon)

@Description
  Entfernt das angegebene SysTray-Icon.

@Parameter "#SysTrayIcon"
  Entfernt das Systray-Icon. Wenn @#PB_All angegeben wird, dann werden alle verbliebenen Systray-Icons freigegeben.
  
@NoReturnValue

@Remarks  
  Alle verbleibenden SysTray-Icons werden automatisch entfernt, wenn das Programm endet.

@SupportedOS
