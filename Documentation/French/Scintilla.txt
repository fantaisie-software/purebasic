;
; Scintilla library documentation
;
; (c) Fantaisie Software
;

@Library Scintilla

@Overview
  Scintilla est un composant libre d'édition 
  de code source. 
  Il est livré avec son propre code source et 
  une licence autorisant son utilisation dans 
  tout projet libre ou commercial. 
  La licence peut être consultée 
  @MainGuideLink "license_scintilla" "ici".
  Le code source, ainsi que la documentation 
  peuvent être trouvés sur le site de 
  @InternetLink "http://www.scintilla.org/" "Scintilla".
@LineBreak
@LineBreak
  @Bold "Extrait du site de Scintilla: " 
  En plus des fonctionnalités classiques des 
  composants d'édition de texte basique, 
  Scintilla inclut des fonctionnalités 
  particulièrement utiles lors de l'édition 
  et du débogage de code source. Cela inclut 
  la stylisation syntaxique, des indicateurs 
  d'erreurs, l'auto-complétion et des 
  indications sur les appels de fonctions.
  La marge de sélection peut contenir des 
  marques similaires à celles utilisées dans 
  les débogueurs pour indiquer les points 
  d'arrêt et la ligne courante.
  Les choix stylistiques sont plus ouverts 
  que la plupart des éditeurs, et permettent 
  l'utilisation de différentes polices, de 
  différentes tailles, les polices grasses 
  et italiques, plusieurs couleurs de texte 
  et de fond, etc...
@LineBreak
@LineBreak
  @Bold "Important:" La licence de Scintilla 
  requiert qu'une indication relative au 
  droit d'auteur soit incluse dans tout 
  logiciel utilisant Scintilla et que le 
  texte de la licence soit fourni dans la 
  documentation du logiciel.
@LineBreak
@LineBreak
  PureBasic intègre la bibliothèque Scintilla 
  avec la bibliothèque 
  @LibraryLink "Gadget" "gadget", si bien que 
  des commandes telles que @@ResizeGadget ou 
  @@HideGadget peuvent être utilisées avec un 
  contrôle scintilla. De plus elle fournit la 
  fonction @@ScintillaSendMessage pour 
  communiquer avec le contrôle pour 
  l'exploiter à son plein potentiel. 
  Toutes les structures et constantes sont 
  définies de base dans PureBasic.
@LineBreak
@LineBreak


@CommandList
 
; @ExampleFile ; TODO

@SupportedOS


;--------------------------------------------------------------------------------------------------------

@Function Resultat = InitScintilla([NomBibliotheque$])

@Deprecated

  Cette commande est obsolète et n’est plus nécessaire.

@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function Resultat = ScintillaGadget(#Scintilla, X, Y, Largeur, Hauteur, @Callback())

@Description
  Crée un nouveau contrôle d'édition scintilla.
  
@Parameter "#Scintilla"  
  Le numéro d'identification du nouveau gadget scintilla.
@LineBreak
@LineBreak  
  @ReferenceLink "purebasic_objects" "#PB_Any" peut être utilisé pour générer automatiquement ce numéro.
  
@Parameter "X, Y, Largeur, Hauteur"  
  La position et les dimensions du nouveau gadget.
  
@Parameter "@Callback()" 
  L'adresse de la procédure qui recevra les évènements émis par le gadget. @LineBreak  
  Elle doit être définie de la façon suivante, avec 'Gadget' qui est le numéro du contrôle 
  et *scinotify qui pointe vers une structure comportant les informations sur l'évènement:  
@Code

  ProcedureDLL ScintillaCallBack(Gadget, *scinotify.SCNotification)
    ;
    ; Votre code ici.
    ;
  EndProcedure
@EndCode

@ReturnValue
  Renvoie une valeur non nulle en cas de succès, zéro sinon.

@Remarks
  Les événements suivants sont pris en charge par @@EventType:
@FixedFont
  @#PB_EventType_RightClick
@EndFixedFont
  
  Les commandes spécifiques de Scintilla peuvent être envoyées au gadget avec la commande @@ScintillaSendMessage .@LineBreak  
  En outre les commandes de gadgets communes comme @@ResizeGadget ou @@HideGadget peuvent aussi bien être utilisées par le contrôle.
 
@Example
@Code
  If OpenWindow(0, 0, 0, 400, 90, "ScintillaGadget", #PB_Window_SystemMenu | #PB_Window_ScreenCentered)
 
          ScintillaGadget(0, 10, 10, 390, 70, 0)
       
          ; Texte en rouge
          ScintillaSendMessage(0, #SCI_STYLESETFORE, 0, RGB(255, 0, 0))
       
          ; Définit le texte initial du ScintillaGadget
          *Texte=UTF8("Hé, voici un simple ScintillaGadget avec du texte...")
          ScintillaSendMessage(0, #SCI_SETTEXT, 0, *Texte)
          FreeMemory(*Texte) ; Le tampon créé par UTF8() doit être libéré pour éviter les fuites de mémoire.
                 
          ; Ajout d'une deuxième ligne de texte précédée d'un saut de ligne
          Texte$ = Chr(10) + "Seconde ligne."
          *Texte=UTF8(Texte$)
          ScintillaSendMessage(0, #SCI_APPENDTEXT, Len(Texte$), *Texte)
          FreeMemory(*Texte)
     
       Repeat : Until WaitWindowEvent() = #PB_Event_CloseWindow
    EndIf
@EndCode

@Image scintillagadget.png

@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function Resultat = ScintillaSendMessage(#Scintilla, Message [, Param [, LParam]])

@Description
  Envoie un message au gadget scintilla pour exécuter une tâche spécifique.

@Parameter "#Scintilla"
  Le numéro d'identification du gadget scintilla.
  
@Parameter "Message"
  Le message à envoyer.
@LineBreak
@LineBreak 
  C'est un nombre entier sous la forme d'une constante prédéfinie qui commence par '#SCI_'.
@LineBreak
@LineBreak 
  Vous trouverez plus d'informations sur les messages possibles sur le 
  @InternetLink "http://www.scintilla.org/" "site Scintilla".
  Les constantes @#SCI_[...] représentant les valeurs possibles pour le 'Message' sont 
  déjà définies dans PB. 

@OptionalParameter "Param"
  Le premier paramètre du message.@LineBreak
  S'il est omis alors la valeur par défaut est zéro.

@OptionalParameter "LParam"
  Le second paramètre du message.@LineBreak
  S'il est omis alors la valeur par défaut est zéro.

@ReturnValue
  Renvoie le résultat du message envoyé.

@Example Texte surligné en rouge
@Code 
  #num_indic = 0
  
  If OpenWindow(0, 0, 0, 330, 90, "ScintillaGadget", #PB_Window_SystemMenu | #PB_Window_ScreenCentered)
  
      ScintillaGadget(0, 5, 5, 320, 80, 0)
      *Texte = UTF8("Ceci est un simple ScintillaGadget avec du texte..." + #LF$ + "Plus de texte" + #LF$ + "Encore plus !")
      ScintillaSendMessage(0, #SCI_SETTEXT, 0, *Texte)
      FreeMemory(*Texte) ; Le tampon créé par UTF8() doit être libéré pour éviter une fuite de mémoire
      
      ScintillaSendMessage(0, #SCI_INDICSETSTYLE, #num_indic, #INDIC_STRAIGHTBOX) ; "indicateur" de style : style  n°8 (0-19)
      ScintillaSendMessage(0, #SCI_INDICSETFORE, #num_indic, #Red)                ; "indicateur" de couleur : rouge
      ScintillaSendMessage(0, #SCI_SETINDICATORCURRENT, #num_indic, #INDIC_STRAIGHTBOX) ; cet "indicateur" est traité comme indicateur en cours
      ScintillaSendMessage(0, #SCI_INDICSETUNDER, #num_indic, 1)   ; l'"indicateur" est derrière le texte, c'est-à-dire ne l'obscurcit pas
      ScintillaSendMessage(0, #SCI_INDICSETALPHA, #num_indic, 127) ; transparence de 127
      nLigne = 1 ; le numéro de ligne commence à 0
      debut = ScintillaSendMessage(0, #SCI_POSITIONFROMLINE, nLigne)
      longueur = ScintillaSendMessage(0, #SCI_GETLINEENDPOSITION, nLigne) - debut
      ScintillaSendMessage(0, #SCI_INDICATORFILLRANGE, debut, longueur) ; Départ et longueur
    
    Repeat : Until WaitWindowEvent() = #PB_Event_CloseWindow
  EndIf
@EndCode

@SupportedOS

;--------------------------------------------------------------------------------------------------------

