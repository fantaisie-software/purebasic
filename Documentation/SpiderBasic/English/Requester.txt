;
;     Requester library documentation
;
;      (c) 2014 - Fantaisie Software
;

@Library Requester

@Overview

Computer users are well used to requesters as almost any graphical application use
at least one of them. They are very handy as some basic tasks (like opening a file) are 
all performed through standard windows called 'requesters'.

@CommandList

@ExampleFile All Requester.sb

@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function Text$ = InputRequester(Message$, DefaultText$)

@Description
  Opens a blocking input requester to enter some text. 
  
@Parameter "Message$"
  Message displayed before the input field.

@Parameter "DefaultText$"
  Default text in the input field.

@ReturnValue
  Returns the text in the input field, or an empty string if the requester has been closed without hitting the "OK" button.

@Example
@Code
  Input$ = InputRequester("Please make your input:", "I'm the default input.")

  If Input$ > ""
    a$ = "You entered in the requester:" + Chr(10)  ; Chr(10) only needed
    a$ + Input$                                                  ; for line-feed
  Else  
    a$ = "The requester was canceled or there was nothing entered."
  EndIf
  Debug a$
@EndCode
@LineBreak
@Image inputrequester.png

@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function Result = MessageRequester(Text$ [, Flags])

@Description
  Opens a blocking requester to display some information. The program execution 
  is totally stopped until the user close the requester.

@Parameter "Text$"
  The text displayed in the requester window.

@OptionalParameter "Flags"
  It can be one of the following value:
@FixedFont
  @#PB_MessageRequester_Ok          : to have the 'ok' only button (default)
  @#PB_MessageRequester_YesNo       : to have 'yes' or 'no' buttons
@EndFixedFont
   

@ReturnValue
  It can be one of the following constants:
@FixedFont
  @#PB_MessageRequester_Yes    : the 'yes' or 'ok' button was pressed
  @#PB_MessageRequester_No     : the 'no' button was pressed
@EndFixedFont

@Example Simple MessageRequester  (usually for information purposes only)
@Code
  MessageRequester("Just a short information text.")
@EndCode

@Example MessageRequester with Yes / No buttons  (usually to question)
@Code
  Result = MessageRequester("Please make your input:", #PB_MessageRequester_YesNo)
  If Result = #PB_MessageRequester_Yes    ; pressed Yes button
    Debug "Yes"
  Else                                    ; pressed No button
    Debug "No"
  EndIf
@EndCode
@LineBreak
@Image messagerequester.png

@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function Result = NextSelectedFile()

@Description
  After a call to @@OpenFileRequester it returns the next selected file (if any). 
  It has to be called in the callback specified in @@OpenFileRequester.
  @@SelectedFilename and @@SelectedFileID can be used to get info about
  the current selected file.

@NoParameters

@ReturnValue
  Returns @#True if there is another file, @#False otherwise.

@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function Result$ = SelectedFileName()

@Description
  Returns the current selected filename. It has to be called after @@NextSelectedFile.
  The filename shouldn't be used directly with other SpiderBasic command, see @@SelectedFileID
  for more information.

@NoParameters

@ReturnValue
  The selected filename. 
  
@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function Result$ = SelectedFileID()

@Description
  Returns the current selected filename identifier. It has to be called after @@NextSelectedFile.
  Remote drives like Google Drive can have several files with the exact same filename, that's why
  a file identifier is needed to be able to select the right one.

@NoParameters

@ReturnValue
  A filename identifier. This identifier can be used in the following commands: 
  @@ReadFile, @@OpenFile, @@LoadJSON, @@LoadImage and @@LoadXML.

@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function Filename$ = OpenFileRequester(Pattern$, Callback [, Flags])

@Description
  Opens the standard requester for the user to choose one or several local files.
  For security reason, the requester can be opened only from an event callback 
  triggered by a real user action (ie: when the user click on a gadget).
  This commands needs to be put in an event procedure, like in the example below.
  The selected file is get with @@NextSelectedFile and @@SelectedFilename in the callback.

@Parameter "Pattern$"
  A standard filter which allow to display only the files which 
  end with such or such extension. It has to be in the MIME form : "image/*",
  "audio/*" etc. A list of most common MIME types can be found here: @InternetLink "http://www.freeformatter.com/mime-types-list.html" "http:://www.freeformatter.com".
  
@Parameter "Callback"
  The callback to be called if the user has selected one or several file. It won't be called
  if the user canceled the requester. It has to use the following syntax:

@Code
  Procedure Callback()
    ; Code here
  EndProcedure
@EndCode

@OptionalParameter "Flags"
  It can be a combination of one of the following values:
@FixedFont
  @#PB_Requester_MultiSelection: Enable the multiselection (see @@NextSelectedFile).
  @#PB_Requester_GoogleDrive: Uses the Google file requester to select a file. @@UseGoogleDrive has to be
                             successfully initialized before using this requester. Note: to be able to open
                             the requester, a domain name needs to be used (ip address won't work), and this domain
                             needs to be allowed in the google developper console. For testing purpose,
                             you can modify your /etc/hosts file and put a domain alias for 127.0.0.1, and
                             then set the 'web server address' compiler option of the SpiderBasic IDE.
                             
@EndFixedFont

@NoReturnValue

@Example
@Code
  Procedure RequesterSuccess()
    
    ; Process all the selected filename
    ;
    While NextSelectedFile()
      Debug "Filename: " + SelectedFileName()
    Wend
  EndProcedure
  
  Procedure ButtonEvent()
    OpenFileRequester("", @RequesterSuccess(), #PB_Requester_MultiSelection)
  EndProcedure
    
  If OpenWindow(0, 100, 100, 200, 55, "File")
    ButtonGadget(0, 10, 10, 170, 25, "Open local file...")
    BindGadgetEvent(0, @ButtonEvent())
  EndIf
@EndCode
@LineBreak
@Image openfilerequester.png

@SeeAlso
  @@NextSelectedFile

@SupportedOS

;--------------------------------------------------------------------------------------------------------

@Function UseGoogleDrive(ClientID$, ApiKey$, Callback)

@Description
  Initializes Google Drive access for a specific application. Only one application can be registered at once.

@Parameter "ClientID$"
  The google client ID of the application which is used to access the drive.
  Each application has it's own ID, for example "568545051626-vlffi940ra93pmb3pl3tq1eaueejje7h"
  for the test application 'SpiderEditor'. To create your own id, you need to go
  to the google developer console.
  
@Parameter "ApiKey$"
  The API key of the application which is used to access the drive.
  Each application has it's own API key, for example "AIzaSyAp_978UH3YhI4czaGaFmWWvN14AJgNfVQ"
  for the test application 'SpiderEditor'. To create your own API key, you need to go
  to the google developer console.

@Parameter "Callback"
  The callback which will be called when everything is ready to use Google Drive. If it is not called,
  then Google Drive is not available. It has to be declared like this:

@Code
  Procedure Callback()
    ; Google drive is ready to use.
  EndProcedure
@EndCode

@NoReturnValue

@Example
@Code
  Procedure GoogleDriveReady()
    Debug "Google drive is ready"
  EndProcedure
  
  ; Uses the SpiderEditor test application
  ;
  UseGoogleDrive("568545051626-vlffi940ra93pmb3pl3tq1eaueejje7h", 
                 "AIzaSyAp_978UH3YhI4czaGaFmWWvN14AJgNfVQ", @GoogleDriveReady())
@EndCode

@SeeAlso
  @@OpenFileRequester

@SupportedOS
